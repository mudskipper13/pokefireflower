const LOCALID_RIVAL = 1
const STAIRS_TRIGGER = 1

mapscripts LittlerootTown_MaysHouse_1F_MapScripts {
	MAP_SCRIPT_ON_LOAD: PlayersHouse_2F_MainInterior_MapScript_OnLoad
}

script PlayersHouse_2F_MainInterior_MapScript_OnLoad {
	call(Common_EventScript_SetupRivalGfxId)
}

script PlayersHouse_2F_MainInterior_TriggerScript_Attics {
	lockall
	msgbox(PlayersHouse_2F_MainInterior_Text_CantGoToAttics, MSGBOX_DEFAULT)
	releaseall
}

script PlayersHouse_2F_MainInterior_TriggerScript_StoryTrigger {
	lockall
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, PlayersHouse_2F_MainInterior_Movement_CameraGoToRivalStairs)
	waitmovement(0)
	addobject(LOCALID_RIVAL)
	clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_RIVAL_SIBLING)
	playse(SE_EXIT)
	waitse
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitfieldeffect(FLDEFF_EMOTE_ICON)
	playbgm(MUS_ENCOUNTER_MAY, FALSE)
	textcolor(NPC_TEXT_COLOR_FEMALE)
	msgbox(PlayersHouse_2F_MainInterior_Text_OhHeyGoodMorning, MSGBOX_DEFAULT)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitfieldeffect(FLDEFF_EMOTE_ICON)
	applymovement(OBJ_EVENT_ID_CAMERA, PlayersHouse_2F_MainInterior_Movement_CameraApproachToStairs)
	applymovement(LOCALID_RIVAL, PlayersHouse_2F_MainInterior_Movement_RivalApproachToStairs)
	msgbox(PlayersHouse_2F_MainInterior_Text_ByTheWay, MSGBOX_DEFAULT)
	textcolor(NPC_TEXT_COLOR_MALE)
	msgbox(PlayersHouse_2F_MainInterior_Text_OhIsThatSo, MSGBOX_DEFAULT)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitfieldeffect(FLDEFF_EMOTE_ICON)
	textcolor(NPC_TEXT_COLOR_FEMALE)
	msgbox(PlayersHouse_2F_MainInterior_Text_WhatYoureJoking, MSGBOX_DEFAULT)
	textcolor(NPC_TEXT_COLOR_MALE)
	msgbox(PlayersHouse_2F_MainInterior_Text_WaitYouMean, MSGBOX_DEFAULT)
	textcolor(NPC_TEXT_COLOR_FEMALE)
	msgbox(PlayersHouse_2F_MainInterior_Text_Yeah, MSGBOX_DEFAULT)
	textcolor(NPC_TEXT_COLOR_MALE)
	msgbox(PlayersHouse_2F_MainInterior_Text_OhRight, MSGBOX_DEFAULT)
	textcolor(NPC_TEXT_COLOR_FEMALE)
	msgbox(PlayersHouse_2F_MainInterior_Text_OhReally, MSGBOX_DEFAULT)
	msgbox(PlayersHouse_2F_MainInterior_Text_GuestsComing, MSGBOX_DEFAULT)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitfieldeffect(FLDEFF_EMOTE_ICON)
	applymovement(LOCALID_RIVAL, Common_Movement_FaceLeft)
	msgbox(PlayersHouse_2F_MainInterior_Text_AlrightMom, MSGBOX_DEFAULT)
	applymovement(LOCALID_RIVAL, Common_Movement_FaceUp)
	msgbox(PlayersHouse_2F_MainInterior_Text_LookAtThat, MSGBOX_DEFAULT)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, PlayersHouse_2F_MainInterior_Movement_PlayerMoveAway)
	waitmovement(OBJ_EVENT_ID_PLAYER)
	applymovement(LOCALID_RIVAL, PlayersHouse_2F_MainInterior_Movement_RivalMoveAway)
	waitmovement(LOCALID_RIVAL)
	playse(SE_EXIT)
	removeobject(LOCALID_RIVAL)
	waitse
	applymovement(OBJ_EVENT_ID_CAMERA, PlayersHouse_2F_MainInterior_Movement_CameraMoveBack)
	waitmovement(OBJ_EVENT_ID_CAMERA)
	fadedefaultbgm
	special(RemoveCameraObject)
	setvar(VAR_LITTLEROOT_INTRO_STATE, 1)
	releaseall
}

movement PlayersHouse_2F_MainInterior_Movement_RivalApproachToStairs {
	walk_right * 3
	walk_up * 2
	delay_16
	delay_16
}

movement PlayersHouse_2F_MainInterior_Movement_CameraApproachToStairs {
	walk_right * 3
	walk_up * 3
	delay_16
	delay_16
}

movement PlayersHouse_2F_MainInterior_Movement_CameraGoToRivalStairs {
	walk_down * 3
	walk_left * 3
	delay_16
	delay_16
}

//! LOL
movement PlayersHouse_2F_MainInterior_Movement_PlayerMoveAway {
	lock_facing_direction
	walk_right
	unlock_facing_direction
	face_left
	delay_16
}

movement PlayersHouse_2F_MainInterior_Movement_RivalMoveAway {
	walk_up
	delay_16
	delay_16
	walk_left
}

movement PlayersHouse_2F_MainInterior_Movement_CameraMoveBack {
	walk_right
	delay_16
	delay_16
}

text PlayersHouse_2F_MainInterior_Text_CantGoToAttics {
	poryswitch(LANG) {
		_:		format(	"{PLAYER}: I can't go to the attics now,"
						"others are waiting for me below!")
		INA:	format(	"{PLAYER}: Aku tidak bisa naik ke loteng"
						"sekarang, yang lain sedang menungguku"
						"dibawah!")
	}
}

text PlayersHouse_2F_MainInterior_Text_OhHeyGoodMorning {
	format(	"LIME: Oh hey,{PAUSE 16} good morning {PLAYER}!\p"
			"I thought you're still"
			"sleeping,{PAUSE 16} heh.")
}

text PlayersHouse_2F_MainInterior_Text_ByTheWay {
	format(	"LIME: By the way,{PAUSE 16} I've heard that GRAMP"
			"MARCUS from STARION is coming over to us"
			"today from MOM earlier.\p"
			"LIME: I wonder when he's going to arrive,{PAUSE 16}"
			"I'm so excited to meet GRAMPs again!")
}

text PlayersHouse_2F_MainInterior_Text_OhIsThatSo {
	format(	"{PLAYER}: Oh…{PAUSE 16} is that so?\p"
			"I don't remember we have a grandpa,{PAUSE 16}"
			"especially from a REGION I've never"
			"heard of,{PAUSE 16} if I'm being honest with you.")
}

text PlayersHouse_2F_MainInterior_Text_WhatYoureJoking {
	format(	"LIME: What?!{PAUSE 16} You're joking,{PAUSE 8} right?{PAUSE 16}\n"
			"How can you NOT remember him?!{PAUSE 30}\p"
			"He's the one that gave us cool stuff"
			"when we're kids and field trips to"
			"STARION,{PAUSE 16} remember?")
}

text PlayersHouse_2F_MainInterior_Text_WaitYouMean {
	format(	"{PLAYER}: Wait,{PAUSE 16} you mean that old man"
			"who has that funny nose and mustache"
			"who comes here with cool stuff?")
}

text PlayersHouse_2F_MainInterior_Text_Yeah {
	format(	"LIME: Y-yeah…")
}

text PlayersHouse_2F_MainInterior_Text_OhRight {
	format(	"{PLAYER}: Oh right,{PAUSE 16} I remember him now!{PAUSE 30}\n"
			"Sorry about that,{PAUSE 16} heh.\p"
			"{PLAYER}: Funnily enough,{PAUSE 16} I actually just somehow"
			"dreamt of him last night,{PAUSE 16} saying that he's"
			"going to PALLET TOWN to meet us.")
}

text PlayersHouse_2F_MainInterior_Text_OhReally {
	format(	"LIME: Oh really?{PAUSE 16}\n"
			"What a coincidence-")
}

text PlayersHouse_2F_MainInterior_Text_GuestsComing {
	format(	"MOM: {PLAYER}!{PAUSE 16} LIME!{PAUSE 16}\n"
			"Get down here right now,{PAUSE 16} we have a special"
			"guest waiting for you both!")
}

text PlayersHouse_2F_MainInterior_Text_AlrightMom {
	format(	"LIME: Alright, MOM!{PAUSE 16}\n"
			"We're coming!")
}

text PlayersHouse_2F_MainInterior_Text_LookAtThat {
	format(	"LIME: Would you look at that,{PAUSE 16} it's"
			"up and coming.\p"
			"LIME: See you downstairs,{PAUSE 16} don't keep"
			"me waiting!{PAUSE 16} Now excuse me.")
}
