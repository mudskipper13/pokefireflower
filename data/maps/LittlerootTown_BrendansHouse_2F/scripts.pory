mapscripts LittlerootTown_BrendansHouse_2F_MapScripts {}

script LittlerootTown_BrendansHouse_2F_EventScript_BeginStory {
	lockall
	setvar(VAR_LITTLEROOT_TOWN_STATE, 1)
	releaseall
}

script LittlerootTown_BrendansHouse_2F_EventScript_Bed {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	msgbox(format("{PLAYER}: Should I rest here?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
		msgbox(format("{PLAYER}: Alright, time to rest."), MSGBOX_AUTOCLOSE)
		call(Common_EventScript_OutOfCenterPartyHeal)
		msgbox(format("{PLAYER}: Ah, better!"), MSGBOX_AUTOCLOSE)
		checkplayergender
		if (var(VAR_RESULT) == FEMALE) {
			applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_WalkLeft)
			waitmovement(0)
		} else {
			applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_WalkRight)
			waitmovement(0)
		}
	} else {
		msgbox(format("{PLAYER}: Hm… Maybe not."), MSGBOX_AUTOCLOSE)
		checkplayergender
		if (var(VAR_RESULT) == FEMALE) {
			applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_WalkRight)
			waitmovement(0)
		} else {
			applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_WalkLeft)
			waitmovement(0)
		}
	}
	releaseall
	end
}

script LittlerootTown_BrendansHouse_2F_EventScript_PC {
	lockall
	setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
	special(DoPCTurnOnEffect)
	playse(SE_PC_ON)
	msgbox(gText_PlayerHouseBootPC, MSGBOX_DEFAULT)
	special(BedroomPC)
	waitstate
	releaseall
}

script LittlerootTown_BrendansHouse_2F_EventScript_TurnOffPlayerPC {
	setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
	playse(SE_PC_OFF)
	special(DoPCTurnOffEffect)
	releaseall
}

script PlayersHouse_2F_EventScript_GameCube {
	msgbox(PlayersHouse_2F_Text_ItsAGameCube, MSGBOX_SIGN)
}

script PlayersHouse_2F_EventScript_Notebook {
	msgbox(PlayersHouse_2F_Text_OhNotebook, MSGBOX_SIGN)
}

movement LittlerootTown_BrendansHouse_2F_Movement_WalkLeft {
	walk_left
}

movement LittlerootTown_BrendansHouse_2F_Movement_WalkRight {
	walk_right
}

text PlayersHouse_2F_Text_ItsAGameCube {
	"It's a Nintendo GameCube.\p"

	"A Game Boy Advance is connected to\n"
	"serve as the controller."
}

text PlayersHouse_2F_Text_OhNotebook {
	"{PLAYER}: Oh, a notebook.\p"

	"I think I should carry it with me."
}

text PlayersHouse_2F_Text_ClockIsStopped {
	"The clock is stopped…\p"

	"Better set and start it!"
}

text Common_Text_LookCloserAtMap {
	"You took a closer look at the\n"
	"KANTO region map."
}
