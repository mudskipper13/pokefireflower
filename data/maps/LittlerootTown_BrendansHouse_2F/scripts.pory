mapscripts LittlerootTown_BrendansHouse_2F_MapScripts {}

script PalletTown_PlayersRoom_EventScript_Bed {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	textcolor(NPC_TEXT_COLOR_PLAYER)
	if (!var(VAR_PALLET_TOWN_STATE)) {
		msgbox(PalletTown_PlayersRoom_Text_CantRestOthersWaiting, MSGBOX_AUTOCLOSE)
		if (var(VAR_RESULT) == FEMALE) {
			applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_PlayersRoom_Movement_WalkRight)
		} else {
			applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_PlayersRoom_Movement_WalkLeft)
		}
		waitmovement(0)
		releaseall
		end
	}
	msgbox(PalletTown_PlayersRoom_Text_ShouldPlayerRest, MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
		msgbox(PalletTown_PlayersRoom_Text_AlrightRestTime, MSGBOX_AUTOCLOSE)
		call(Common_EventScript_OutOfCenterPartyHeal)
		msgbox(PalletTown_PlayersRoom_Text_AhBetter, MSGBOX_AUTOCLOSE)
		checkplayergender
		if (var(VAR_RESULT) == FEMALE) {
			applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_PlayersRoom_Movement_WalkLeft)
		} else {
			applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_PlayersRoom_Movement_WalkRight)
		}
		waitmovement(0)
	} else {
		msgbox(PalletTown_PlayersRoom_Text_HmMaybeNot, MSGBOX_AUTOCLOSE)
		checkplayergender
		if (var(VAR_RESULT) == FEMALE) {
			applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_PlayersRoom_Movement_WalkRight)
		} else {
			applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_PlayersRoom_Movement_WalkLeft)
		}
		waitmovement(0)
	}
	releaseall
}

script LittlerootTown_BrendansHouse_2F_EventScript_PC {
	lockall
	setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
	special(DoPCTurnOnEffect)
	playse(SE_PC_ON)
	msgbox(gText_PlayerHouseBootPC, MSGBOX_DEFAULT)
	special(BedroomPC)
	waitstate
	releaseall
}

script LittlerootTown_BrendansHouse_2F_EventScript_TurnOffPlayerPC {
	setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
	playse(SE_PC_OFF)
	special(DoPCTurnOffEffect)
	releaseall
}

script PlayersHouse_2F_EventScript_GameCube {
	msgbox(PalletTown_PlayersRoom_Text_ItsAGameCube, MSGBOX_SIGN)
}

script PlayersHouse_2F_EventScript_Notebook {
	msgbox(PalletTown_PlayersRoom_Text_OhNotebook, MSGBOX_SIGN)
}

movement PalletTown_PlayersRoom_Movement_WalkLeft {
	walk_left
}

movement PalletTown_PlayersRoom_Movement_WalkRight {
	walk_right
}

text PalletTown_PlayersRoom_Text_ItsAGameCube {
	poryswitch(LANG) {
		_ {
			format(	"It's a Nintendo GameCube.\p"
					"A Game Boy Advance is connected to\n"
					"serve as the controller.")
		}
		INA {
			format(	"Ini adalah sebuah Nintendo GameCube.\p"
					"Sebuah Game Boy Advance dicolokkan\n"
					"ke dalamnya dan digunakan sebagai"
					"pengontrol.")
		}
	}
}

text PalletTown_PlayersRoom_Text_OhNotebook {
	poryswitch(LANG) {
		_ {
			format(	"{PLAYER}: Oh, my notebook.\p"
					"Maybe I should carry it with me"
					"later.")
		}
		INA {
			format(	"{PLAYER}: Oh, buku catatanku.\p"
					"Mungkin aku harus membawanya"
					"denganku nanti.")
		}
	}
}

text PlayersHouse_2F_Text_ClockIsStopped {
	poryswitch(LANG) {
		_ {
			format(	"The clock is stopped…\p"
					"Better set and start it!")
		}
		INA {
			format(	"Jam dindingnya mati…\p"
					"Sebaiknya diatur dan dinyalakan!")
		}
	}
}

text Common_Text_LookCloserAtMap {
	poryswitch(LANG) {
		_ {
			format(	"You took a closer look at the\n"
					"KANTO region map.")
		}
		INA {
			format(	"Kamu meneliti lebih dekat ke map"
					"daerah KANTO.")
		}
	}
}

text PalletTown_PlayersRoom_Text_ShouldPlayerRest {
	poryswitch(LANG) {
		_:		format("{PLAYER}: Should we rest here?")
		INA:	format("{PLAYER}: Apakah kita istirahat disini?")
	}
}

text PalletTown_PlayersRoom_Text_AlrightRestTime {
	poryswitch(LANG) {
		_:		format("{PLAYER}: Alright, time to rest.")
		INA:	format("{PLAYER}: Baiklah, saatnya istirahat.")
	}
}

text PalletTown_PlayersRoom_Text_AhBetter {
	poryswitch(LANG) {
		_:		format("{PLAYER}: Ah, better!")
		INA:	format("{PLAYER}: Ah, lebih baik!")
	}
}

text PalletTown_PlayersRoom_Text_HmMaybeNot {
	poryswitch(LANG) {
		_:		format("{PLAYER}: Hm… Maybe not.")
		INA:	format("{PLAYER}: Hm… Mungkin tidak.")
	}
}

text PalletTown_PlayersRoom_Text_CantRestOthersWaiting {
	poryswitch(LANG) {
		_:		format(	"{PLAYER}: I can't rest now,"
						"others are waiting for me below!")
		INA:	format(	"{PLAYER}: Aku gak bisa tidur sekarang,"
						"yang lain sedang menungguku dibawah!")
	}
}
